import pandas as pd
from panagram.index import Index
from panagram.index import EXTRA_DIR

configfile: "config.yaml"

index = Index(".", mode="w")
index.load_config()

SAMPLES = index.samples #pd.read_table(config["samples"]).set_index("name")

TMPDIR = f"tmp/"
KMC_EXTS = ["kmc_pre","kmc_suf"]

def get_fasta(wildcards):
    f = SAMPLES.loc[wildcards.sample, "fasta"]
    return f

def get_fai(wildcards):
    return get_fasta(wildcards)+".fai"

def get_genome_id(wildcards):
    f = SAMPLES.loc[wildcards.sample, "id"]
    return f

def get_onehot_tag(wildcards):
    i = SAMPLES.index.get_loc(wildcards.sample) % 32
    return 1 << i

rule anchor:
    input:
        kmc_pre=expand("kmc/bitvec{i}.kmc_pre", i=range(index.kmc_bitvec_count)),
        kmc_suf=expand("kmc/bitvec{i}.kmc_suf", i=range(index.kmc_bitvec_count)),
        fasta=get_fasta,
    output:
        expand("anchor/{{sample}}/bitmap.{step}.{ext}", step=index.steps, ext=["gz","gzi"]),
        "anchor/{sample}/chrs.tsv",
    log:
        logfile="logs/anchor.{sample}.txt"
    run:
        index[wildcards.sample].run_anchor(index.bitvec_prefixes, log.logfile)

rule kmc_bitvec:
    input:
        "kmc/opdef{i}.txt",
    output:
        expand("kmc/bitvec{{i}}.{ext}", ext=KMC_EXTS)
    log:
        "logs/kmc.bitvec{i}.txt"
    shell:
        f"{EXTRA_DIR}/kmc_tools complex kmc/opdef{{wildcards.i}}.txt > {{log}} 2>&1"

rule opdefs:
    input:
        expand("kmc/{sample}.onehot.{ext}", sample=list(SAMPLES.index), ext=KMC_EXTS)
    output:
        expand("kmc/opdef{i}.txt", i=range(index.kmc_bitvec_count))
    run:
        index.init_opdefs()

rule kmc_sample:
    input:
        get_fasta #"{fasta}"
    output:
        expand("kmc/{{sample}}.{db}.{ext}", db=["count","onehot"], ext=KMC_EXTS)
    params:
        tag = get_onehot_tag
    log:
        "logs/kmc.{sample}.txt"
    threads : config["kmc"]["threads"]
    shell:
        f"mkdir -p {TMPDIR}{{wildcards.sample}}; "

        f"{EXTRA_DIR}/kmc -k{{config[k]}} -t{{threads}} -m{{config[kmc][memory]}} "
        f"-ci1 -cs1000 -fm {{input}} kmc/{{wildcards.sample}}.count {TMPDIR}{{wildcards.sample}} "
        "> {log} 2>&1;"

        f"{EXTRA_DIR}/kmc_tools -t4 transform kmc/{{wildcards.sample}}.count " 
        f"set_counts {{params.tag}} kmc/{{wildcards.sample}}.onehot "
        ">> {log} 2>&1;"

rule faidx:
    input:
        "{fasta}"
    output:
        "{fasta}.fai"
    log:
        "logs/faidx.{fasta}.txt"
    shell:
        "samtools faidx {input} > {log} 2>&1"

rule mash_sample:
    input:
        get_fasta
    output:
        "{sample}.msh"
    log:
        "logs/mash.sketch.{sample}.txt"
    shell:
        "{EXTRA_DIR}/mash "
        "sketch -C {wildcards.sample} -o {wildcards.sample}.msh -r -s 10000 {input} "
        "> {log} 2>&1;"


rule mash_triangle:
    input:
        expand("{sample}.msh", sample=SAMPLES.index)
    output:
        "genome_dist.tsv"
    log:
        "logs/mash.triangle.txt"
    shell:
        "{EXTRA_DIR}/mash "
        "triangle -C -E {input} > {output} 2> {log}"
        
rule all:
    input:
        "genome_dist.tsv",
        #index.anchor_filenames
        expand("anchor/{sample}/chrs.tsv", sample=index.anchor_genomes)
